apply plugin: 'application'

java.sourceCompatibility = JavaVersion.VERSION_11
java.targetCompatibility = JavaVersion.VERSION_11

dependencies {
    implementation project(":mapsforge-map-gtk")
    implementation project(":mapsforge-themes")
    testImplementation "junit:junit:$jUnitVersion"
}

repositories {
    maven { url 'https://jitpack.io' }
    mavenLocal()
}

mainClassName = "org.mapsforge.samples.gtk.SampleApp"

run {
    if (project.hasProperty("args")) {
        args project.getProperty("args").split(",")
    }
}

task fatJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'jar-with-dependencies'
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    manifest {
        attributes 'Main-Class': "${mainClassName}"
    }
    with jar
}
