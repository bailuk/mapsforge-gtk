apply plugin: 'application'

sourceCompatibility = 11
targetCompatibility = 11

dependencies {
    implementation project(":mapsforge-map-gtk")
    implementation project(":mapsforge-themes")
    implementation 'ch.bailu.java-gtk:library:0.1.0-SNAPSHOT'
    testImplementation "junit:junit:$jUnitVersion"
}

repositories {
    mavenLocal()
}

mainClassName = "org.mapsforge.samples.gtk.Samples"

run {
    if (project.hasProperty("args")) {
        args project.getProperty("args").split(",")
    }
}

task fatJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'jar-with-dependencies'
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    manifest {
        attributes 'Main-Class': "${mainClassName}"
    }
    with jar
}
